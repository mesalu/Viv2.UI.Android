// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    afterEvaluate {
        // Load a keystore properties file that is omitted from version control (so we don't
        // need to include sensitive strings in version control.)
        def keystoreInfoFile = rootProject.file("keystore.properties");
        def keystoreProps = new Properties()


        if (keystoreInfoFile.exists() && project.hasProperty("android") && android.signingConfigs.hasProperty('release')) {
            keystoreProps.load(new FileInputStream(keystoreInfoFile))
            android.signingConfigs['release'].storeFile = file(keystoreProps['storeFile'])
            android.signingConfigs['release'].storePassword = keystoreProps['storePassword']
            android.signingConfigs['release'].keyAlias = keystoreProps['keyAlias']
            android.signingConfigs['release'].keyPassword = keystoreProps['keyPassword']
        }
    }

    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}